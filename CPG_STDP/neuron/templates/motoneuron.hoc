begintemplate motoneuron
public soma, axon, dend, postsyn, presyn, connect2target

create soma, axon, dend[1] 
objectvar postsyn[1]

public synlistex, synlistinh
objref synlistex, synlistinh
    objref synex_
    objref syninh_

proc init() {local i, j
    ndend = 10          
    nsyn = 16
    soma_diam = 20     
    soma_len = 20      
    soma_nseg = 1
    soma_cm = 2
    soma_Ra = 200     

    axon_diam = 2      
    axon_len = 200     
    axon_nseg = 50 
    axon_cm = 2
    axon_Ra = 0.002        

    dend_diam = 1      
    dend_len = 50      
    dend_nseg = 1
    dend_cm = 2
    dend_Ra = 200         

    create soma, axon, dend[ndend] 

    synlistex = new List()
    synlistinh = new List()

    soma {
        nseg = soma_nseg    
        diam = soma_diam    
        L = soma_len 
        cm = soma_cm
        Ra = soma_Ra
        insert hh
        gnabar_hh=0.3
        gl_hh = .00017
        el_hh = -60.0
        pt3dclear()
        pt3dadd(-500,150,$1,soma_diam) 
        pt3dadd(-500-soma_len,150,$1,soma_diam) 

        insert extracellular
        xg = 1000
        xc = 300
    }

    axon {
        nseg = axon_nseg    
        diam = axon_diam    
        L = axon_len 
        cm = axon_cm
        Ra = axon_Ra       

        insert hh

        pt3dclear()     
        pt3dadd(-500,150,$1,axon_diam) 
        pt3dadd(-500-soma_len-axon_len,150,$1,axon_diam) 
    }


    for i = 0, ndend-1{
        dend[i] {
            nseg = dend_nseg    
            diam = dend_diam    
            L = dend_len        
            pt3dclear()     
            pt3dadd(-500+dend_len*i,150,$1,diam)
            pt3dadd(-500+dend_len*(i+1),150,$1,diam)
            insert pas
            g_pas = 7.7e-6 //FLESHMAN ET AL 1988 - cells 35/4
            e_pas = -70.0
        }

        for j = 0, nsyn*3-1{
            dend[i] syninh_ = new  Exp2Syn (0.5)   synlistinh.append(syninh_)
            syninh_.e = -70
            syninh_.tau1 = 1.5
            syninh_.tau2 = 2
            dend[i] synex_ = new ExpSyn(0.5)  synlistex.append(synex_)
            synex_.tau = 0.1
            synex_.e=50
        }

    }

    connect axon(0), soma(1)    
    connect dend[0](0), soma(1)
    for i = 1, ndend-1 connect dend[i](0), dend[i-1](1)
}

obfunc connect2target() { localobj nc //$o1 target point process, optional $o2 returned NetCon
    soma nc = new NetCon(&v(1), $o1)
    nc.threshold = 10
    if (numarg() == 2) { $o2 = nc } // for backward compatibility
    return nc
}

endtemplate motoneuron